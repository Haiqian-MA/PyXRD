# Contributor: Runar Tenfjord < runar dot tenfjord at gmail dot com >

_realname=matplotlib
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=2.2.2
pkgrel=1
pkgdesc="A python plotting library, making publication quality plots (mingw-w64)"
arch=('any')
url='https://matplotlib.org/'
license=('custom')
makedepends=(
            "${MINGW_PACKAGE_PREFIX}-python3"
            "${MINGW_PACKAGE_PREFIX}-python3-setuptools"
            "${MINGW_PACKAGE_PREFIX}-python3-pytz"
            "${MINGW_PACKAGE_PREFIX}-python3-numpy"
            "${MINGW_PACKAGE_PREFIX}-python3-cairo"
            "${MINGW_PACKAGE_PREFIX}-python3-dateutil"
            "${MINGW_PACKAGE_PREFIX}-python3-pyparsing"
            "${MINGW_PACKAGE_PREFIX}-python3-cycler"
            "${MINGW_PACKAGE_PREFIX}-python3-kiwisolver"
            "${MINGW_PACKAGE_PREFIX}-freetype"
            "${MINGW_PACKAGE_PREFIX}-libpng"
            #"${MINGW_PACKAGE_PREFIX}-qhull"
            )
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/matplotlib/matplotlib/archive/v${pkgver}.tar.gz"
        setupext.py.patch
        setup.py.patch)
sha256sums=('3a8236e6747dbb48c342ee194a8ced2805205cbd718899b284e4d617189cd397'
            '7ecd678f2325486fc79998dd11f79a5c0e0284d3c4260678f47c4b24d0a3d893'
            '2ace7d4fe23ba9e96f2c81f827c8ea6bc65c532e6bd4eee3a655aae1ae6dab17')
noextract=("${_realname}-${pkgver}.tar.gz")

prepare() {
  # Clean up old sources so re-patching doesn't fail.
  [[ -d ${srcdir}/${_realname}-${pkgver} ]] && rm -rf $srcdir/${_realname}-${pkgver}
  tar -xzvf ${_realname}-${pkgver}.tar.gz -C ${srcdir}

  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/setupext.py.patch
  patch -p1 -i ${srcdir}/setup.py.patch
  cd ${srcdir}

  cp -a ${_realname}-${pkgver} ${_realname}-py2-${pkgver}
  cp -a ${_realname}-${pkgver} ${_realname}-py3-${pkgver}

  cd ${srcdir}/${_realname}-py3-${pkgver}
  # remove cairo which fails for python3
  sed -i 's/setupext\.BackendGtk3Cairo/#setupext\.BackendGtk3Cairo/g' setup.py
  sed -i 's/setupext\.BackendCairo/#setupext\.BackendCairo/g' setup.py

}

build() {
  cd ${srcdir}/${_realname}-py3-${pkgver}
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package_python3-matplotlib() {
    depends=(
        "${MINGW_PACKAGE_PREFIX}-python3-pytz"
        "${MINGW_PACKAGE_PREFIX}-python3-numpy"
        "${MINGW_PACKAGE_PREFIX}-python3-cairo"
        "${MINGW_PACKAGE_PREFIX}-python3-cycler"
        "${MINGW_PACKAGE_PREFIX}-python3-dateutil"
        "${MINGW_PACKAGE_PREFIX}-python3-pyparsing"
        "${MINGW_PACKAGE_PREFIX}-python3-kiwisolver"
		"${MINGW_PACKAGE_PREFIX}-python3-gobject"
		"${MINGW_PACKAGE_PREFIX}-gtk3"
        "${MINGW_PACKAGE_PREFIX}-freetype"
        "${MINGW_PACKAGE_PREFIX}-libpng"
        #"${MINGW_PACKAGE_PREFIX}-qhull"
    )
   cd ${srcdir}/${_realname}-py3-${pkgver}

   MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
   ${MINGW_PREFIX}/bin/python3 setup.py install -O1 --skip-build \
   --root="${pkgdir}" --prefix=${MINGW_PREFIX}

   install -Dm644 doc/users/license.rst "${pkgdir}${MINGW_PREFIX}"/share/licenses/python3-matplotlib/LICENSE
}

package_mingw-w64-i686-python3-matplotlib() {
  package_python3-matplotlib
}

package_mingw-w64-x86_64-python3-matplotlib() {
  package_python3-matplotlib
}

# vim:set ts=2 sw=2 et:
